/*
 * Copyright (C) 2017-2018 Manbang Group
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: "version.gradle"

buildscript {
    repositories {
//        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/'
        }
        jcenter()
        google()
        mavenCentral()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.4"

        classpath 'com.wlqq.phantom:phantom-host-gradle:3.1.2'
        classpath 'com.wlqq.phantom:phantom-plugin-gradle:3.1.2'

        classpath 'com.novoda:bintray-release:0.9'
    }
}

allprojects {
    repositories {
//        mavenLocal()
        maven {
            url 'http://maven.aliyun.com/'
        }
        jcenter()
        google()
        mavenCentral()
    }

    tasks.withType(Javadoc) {
        options {
            encoding 'UTF-8'
            charSet 'UTF-8'
            links 'http://docs.oracle.com/javase/7/docs/api'
        }
    }
}

subprojects { subProject ->
    subProject.tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc).all { enabled = false }

    afterEvaluate { project ->
        if (project.hasProperty("android")) {
            android {
                compileSdkVersion androidVersion.compileSdk
                buildToolsVersion androidVersion.buildTools

                defaultConfig {
                    minSdkVersion androidVersion.minSdk
                    targetSdkVersion androidVersion.targetSdk
                }

                lintOptions {
                    checkReleaseBuilds false
                    abortOnError false
                }

                packagingOptions {
                    exclude 'META-INF/LGPL2.1'
                    exclude 'META-INF/LICENSE'
                    exclude 'META-INF/LICENSE.txt'
                    exclude 'META-INF/NOTICE'
                    exclude 'META-INF/NOTICE.txt'
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_1_7
                    targetCompatibility JavaVersion.VERSION_1_7
                }
            }
        }
    }
}
